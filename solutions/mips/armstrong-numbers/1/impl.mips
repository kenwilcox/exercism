# | Register | Usage     | Type    | Description                                       |
# | -------- | --------- | ------- | ------------------------------------------------- |
# | `$a0`    | input     | address | number                                            |
# | `$v0`    | output    | boolean | is armstrong number (`0` = `false`, `1` = `true`) |
# | `$t0-9`  | temporary | any     | used for temporary storage                        |

.globl is_armstrong_number

is_armstrong_number:
        beqz $a0, __equal
        move $t0, $a0
        move $t9, $zero

__count_digits:
        div  $t0, $t0, 10
        addi $t9, $t9, 1
        bnez $t0, __count_digits

        move $t0, $a0
        move $t8, $zero

__sum:
        rem  $t1, $t0, 10
        div  $t0, $t0, 10
        move $t3, $t9
        addi $t4, $zero, 1

__power:
        mul  $t4, $t4, $t1
        subi $t3, $t3, 1
        bnez $t3, __power
        add  $t8, $t8, $t4
        bnez $t0, __sum

__equal:
        seq  $v0, $a0, $t8
        jr   $ra